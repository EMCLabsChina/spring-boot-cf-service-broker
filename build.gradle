buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.1.9.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'maven-publish'

version = "2.3.3"
group = "org.cloudfoundry"
description = "Spring Boot project for creating Cloud Foundry service brokers"

archivesBaseName = "spring-boot-cf-service-broker" 

ext {
    springBootVersion = '1.1.9.RELEASE'
    hibernateValidatorVersion = '5.1.0.Final'
    jsonPathVersion = '0.9.1'

    baseNameTests = "${project.archivesBaseName}-tests"
}

task testsJar(type: Jar) {
    from sourceSets.test.output
    baseName = baseNameTests
}

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-security:${springBootVersion}")
    compile("org.hibernate:hibernate-validator:${hibernateValidatorVersion}")

    testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    testCompile("com.jayway.jsonpath:json-path:${jsonPathVersion}")
}

publishing {
    publications {
        jar(MavenPublication) {
            groupId group
            artifactId project.archivesBaseName
            version version
            from components.java
        }
        test(MavenPublication) {
            groupId group
            artifactId baseNameTests
            version version
            artifact testsJar
        }
    }
    if (project.hasProperty("artifactory_contextUrl")) {
        repositories {
            maven {
                credentials {
                    username "${artifactory_user}"
                    password "${artifactory_password}"
                }
                url "${artifactory_contextUrl}/libs-release-local"
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}
